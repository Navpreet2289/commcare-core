apply plugin: 'java'

// Needed even though it looks unused
sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.3.1"
    }
}

apply plugin: 'me.champeau.gradle.jmh'

test {
  reports.junitXml.destination = file('build/reports/tests')
}

sourceSets {
    translate {
        java {
            compileClasspath += main.output
        }
    }

  cli {
      java {
          compileClasspath += main.output
          compileClasspath += translate.output
      }
  }
    api {
        java {
            srcDir 'src/cli/java/org/commcare/util/screen'
            srcDir 'src/cli/java/org/commcare/util/engine'
            compileClasspath += main.output
        }
    }

    test {
        java {
            compileClasspath += cli.output
            compileClasspath += api.output
            compileClasspath += translate.output
        }
    }


}

dependencies {
      compile 'kxml2:kxml2:2.3.0'
      implementation group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1'
      implementation files('lib/regexp-me.jar')
      // Added so Android Studio recognizes libs in util jar projects
      implementation 'org.json:json:20140107'
      implementation 'commons-cli:commons-cli:1.3.1'
      implementation 'xpp3:xpp3:1.1.4c'
      implementation 'joda-time:joda-time:2.9.3'
      implementation 'com.carrotsearch:hppc:0.7.2'
      compile "com.squareup.retrofit2:retrofit:2.3.0"
      implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'
      compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
      compile 'io.reactivex.rxjava2:rxjava:2.1.1'


      cliImplementation 'org.json:json:20140107'
      cliImplementation files('lib/regexp-me.jar')
      cliCompile 'kxml2:kxml2:2.3.0'
      cliImplementation 'commons-cli:commons-cli:1.3.1'
      cliImplementation 'xpp3:xpp3:1.1.4c'
      cliImplementation 'joda-time:joda-time:2.9.3'
      cliCompile 'io.reactivex.rxjava2:rxjava:2.1.1'

      testImplementation sourceSets.main.output
      testImplementation sourceSets.api.output
      testImplementation sourceSets.cli.output
      testImplementation 'junit:junit:4.12'
      testImplementation 'org.json:json:20140107'
      testImplementation 'org.xerial:sqlite-jdbc:3.8.11.1'
      testImplementation 'joda-time:joda-time:2.9.3'

      apiImplementation sourceSets.main.output
      apiImplementation 'org.json:json:20140107'
      apiImplementation 'org.xerial:sqlite-jdbc:3.8.11.1'
      apiCompile 'kxml2:kxml2:2.3.0'
      apiImplementation 'joda-time:joda-time:2.9.3'
      apiCompile 'io.reactivex.rxjava2:rxjava:2.1.1'

      translateCompile 'kxml2:kxml2:2.3.0'
      translateImplementation 'xpp3:xpp3:1.1.4c'
      translateImplementation 'commons-cli:commons-cli:1.3.1'
      translateImplementation files('lib/regexp-me.jar')
      translateImplementation files('lib/json-simple-1.1.1.jar')
      translateImplementation 'joda-time:joda-time:2.9.3'
      translateImplementation 'org.json:json:20140107'
      translateCompile 'io.reactivex.rxjava2:rxjava:2.1.1'

    // 'compile' dependency is also needed to run 'gradle jmh' for some reason,
    // but leaving it in conflicts with apk builds, so only uncomment the
    // following lines when jmh needed:
    /*
    compile 'org.openjdk.jmh:jmh-core:1.17.3'
    compile 'org.openjdk.jmh:jmh-generator-annprocess:1.17.3'
    jmhCompile 'org.openjdk.jmh:jmh-core:1.17.3'
    jmhCompile 'org.openjdk.jmh:jmh-generator-annprocess:1.17.3'
    */
}

jar {
    baseName = "commcare-libraries"
}

jar.dependsOn test

configurations {
  api
  cliOutput.extendsFrom (cliCompile)
  harness
  translate
  testsAsJar
}

task cliJar(type: Jar, dependsOn: cliClasses) {
    baseName = "commcare-cli"
    from sourceSets.translate.output
    from sourceSets.cli.output
    from sourceSets.main.output
    from {
        configurations.cliCompile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.cliRuntime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'org.commcare.util.cli.CliMain'
    }

}

task apiJar(type: Jar, dependsOn: apiClasses) {
    baseName = "commcare-api"
    from sourceSets.api.output
    from sourceSets.main.output
    from {
        configurations.apiCompile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.apiRuntime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

task harnessJar(type: Jar, dependsOn: translateClasses) {
    baseName = "javarosa-cli"
    from sourceSets.translate.output
    from sourceSets.main.output
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'org.javarosa.engine.Harness'
    }
}

// used to provide the test source files to external projects, such as commcare-android,
// which might want to import classes
task testsrcJar(type: Jar, dependsOn: testClasses) {
    classifier = 'tests'
    from files(sourceSets.test.output.classesDir)
    from files(sourceSets.cli.output.classesDir)
}


task formTranslateJar(type: Jar, dependsOn: translateClasses) {
    baseName = "form_translate"
    from sourceSets.translate.output
    from sourceSets.main.output
    from {
        configurations.translateCompile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.translateRuntime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'org.javarosa.xform.schema.Harness'
    }
}

artifacts {
  cliOutput cliJar
  api apiJar
  harness harnessJar
  translate formTranslateJar
  testsAsJar testsrcJar
}

task copyTestResources(type: Copy) {
    from sourceSets.test.resources
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

def buildDeps = hasProperty('javarosaDeps') ? javarosaDeps : null

/* for use primarily by the build server to extract the required 3rd-party
libraries into the javarosa source tree. to use this in your own build, set the
'extract-libs-fresh' property, then also set the 'javarosa-deps' property to
the path of the library archive (make sure you are using the version of the
archive compatible with the version you wish to build) */
task extractLibs(type: Copy) {
    if (buildDeps != null) {
        from zipTree(file("${buildDeps}")) into file("${projectDir}")
    }
}

compileJava.dependsOn extractLibs


task jenkinsTest {
    inputs.files test.outputs.files
    doLast {
        def timestamp = System.currentTimeMillis()
        test.getReports().getJunitXml().getDestination().eachFile { it.lastModified = timestamp }
    }
}

check.dependsOn(jenkinsTest)

jmh {
    duplicateClassesStrategy = 'warn'
    fork = 2
    iterations = 10
    warmupIterations = 20
    includeTests = true
    // default mode is 'throughput'; where a larger score is better
}
